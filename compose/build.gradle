apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'com.vanniktech.maven.publish'

sourceSets {
	main {
		kotlin {
			srcDir 'upstream/compose/runtime/runtime/src/commonMain/kotlin'
			srcDir 'upstream/compose/runtime/runtime-dispatch/src/commonMain/kotlin'
			srcDir 'upstream/compose/runtime/runtime/src/jvmMain/kotlin'
		}
	}
	test {
		kotlin {
			srcDir 'upstream/compose/runtime/runtime/src/test/kotlin'
		}
	}
}

dependencies {
	api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
	api 'org.jetbrains.kotlinx:kotlinx-collections-immutable:0.3.3'

	kotlinPlugin deps.composeCompiler

	testImplementation 'org.jetbrains.kotlin:kotlin-test-multiplatform'
}

test.filter {
	// TODO these rely on the global EmbeddingContext to function.
	excludeTest 'androidx.compose.runtime.CompositionTests', null
	// TODO flaky test https://issuetracker.google.com/issues/169406779
	excludeTest 'androidx.compose.runtime.snapshots.DerivedSnapshotStateTests', 'multipleSnapshotsAreIsolatedAndCanBeApplied'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
	kotlinOptions.freeCompilerArgs += [
		"-Xmulti-platform",
	]
}

if (!file('upstream/.git').exists()) {
	throw new RuntimeException(
		"Missing 'upstream' git submodule clone. Did you run 'git submodule update --init'?")
}
