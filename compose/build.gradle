apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
	jvm()

	sourceSets {
		commonMain {
			kotlin {
				srcDir 'upstream/compose/runtime/runtime/src/commonMain/kotlin'
				srcDir 'upstream/compose/runtime/runtime-dispatch/src/commonMain/kotlin'
			}
			dependencies {
				api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
				api 'org.jetbrains.kotlinx:kotlinx-collections-immutable:0.3.3'
			}
		}
		jvmMain {
			kotlin {
				srcDir 'upstream/compose/runtime/runtime/src/jvmMain/kotlin'
			}
		}
	}
}

configurations {
	kotlinPlugin
}

dependencies {
	kotlinPlugin 'androidx.compose:compose-compiler:1.0.0-alpha03'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
	kotlinOptions {
		useIR = true
	}

	def pluginConfiguration = configurations.kotlinPlugin
	dependsOn(pluginConfiguration)
	doFirst {
		if (!file('upstream/.git').exists()) {
			throw new RuntimeException(
				"Missing 'upstream' git submodule clone. Did you run 'git submodule update --init'?")
		}

		if (!pluginConfiguration.isEmpty()) {
			kotlinOptions.freeCompilerArgs +=
				"-Xplugin=${pluginConfiguration.files.first()}"
		}
	}
}
