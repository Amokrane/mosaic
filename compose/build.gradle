apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
	jvm()

	sourceSets {
		commonMain {
			kotlin {
				srcDir 'upstream/compose/runtime/runtime/src/commonMain/kotlin'
				srcDir 'upstream/compose/runtime/runtime-dispatch/src/commonMain/kotlin'
			}
			dependencies {
				api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
				api 'org.jetbrains.kotlinx:kotlinx-collections-immutable:0.3.3'
			}
		}
		commonTest {
			kotlin {
				srcDir 'upstream/compose/runtime/runtime/src/test/kotlin'
			}
			dependencies {
				implementation 'org.jetbrains.kotlin:kotlin-test-multiplatform'
			}
		}
		jvmMain {
			kotlin {
				srcDir 'upstream/compose/runtime/runtime/src/jvmMain/kotlin'
			}
		}
	}
}

tasks.named('jvmTest').configure { Test test ->
	test.filter {
		// TODO these rely on the global EmbeddingContext to function.
		excludeTest 'androidx.compose.runtime.CompositionTests', null
	}
}

dependencies {
	kotlinPlugin deps.composeCompiler
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
	doFirst {
		if (!file('upstream/.git').exists()) {
			throw new RuntimeException(
				"Missing 'upstream' git submodule clone. Did you run 'git submodule update --init'?")
		}
	}
}
